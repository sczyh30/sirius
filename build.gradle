configure(allprojects) { project ->

  apply plugin: 'java'
  apply plugin: 'idea'

  group 'io.sczyh30.sirius'
  version '0.1-SNAPSHOT'

  ext {
    assertjVersion = "3.6.1"
    nettyVersion = "4.1.7.Final"
  }

  repositories {
    jcenter()
  }

  dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.assertj', name: 'assertj-core', version: assertjVersion
  }

  sourceSets {
    main {
      java {
        srcDirs += 'src/main/generated'
      }
    }
  }

  compileJava {
    targetCompatibility = 1.8
    sourceCompatibility = 1.8
  }
}

project("sirius-core") {
  dependencies {
    compile group: 'io.netty', name: 'netty-all', version: nettyVersion
  }
}

project("sirius-web") {
  dependencies {
    compile(project(":sirius-core"))
  }
}

project("sirius-rpc") {
  dependencies {
    compile(project(":sirius-core"))
  }
}

project("sirius-service-discovery") {
  dependencies {
    compile(project(":sirius-core"))
  }
}

project("sirius-circuit-breaker") {
  dependencies {
    compile(project(":sirius-core"))
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '3.2'
}
